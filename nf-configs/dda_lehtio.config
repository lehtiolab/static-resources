external_config_version = 'main'

includeConfig "https://raw.githubusercontent.com/lehtiolab/static-resources/${external_config_version}/nf-configs/lehtio.config"

def listify(it) {
  /* This function is useful when needing a list even when having a single item
  - Single items in channels get unpacked from a list
  - Processes expect lists. Even though it would be fine
  without a list, for single-item-lists any special characters are not escaped by NF
  in the script, which leads to errors. See:
  https://github.com/nextflow-io/nextflow/discussions/4240
  */
  return it instanceof java.util.List ? it : [it]
}


process {
  withName: complementSpectraLookupCleanPSMs {
    scratch = true
  }
  withName: createNewSpectraLookup {
    time = { mzmlfiles.size() > 100 ? check_max( 0.02.h * mzmlfiles.size() * task.attempt, 'time' ) : 2.h * task.attempt}
    scratch = true
  }
  withName: quantifySpectra {
    maxForks = 10
    memory = { (infile.size() >> 30) > 5 ? check_max(infile.size() * 4 as nextflow.util.MemoryUnit, 'memory') : '16 GB'}
    time = { check_max( 1.h * (infile.size() >> 30) * task.attempt, 'time' ) }
  }
  withName: quantifyMS1 {
    cpus = 4
  }
  withName: msgfPlus {
    maxForks = 20
    cpus = 4
    memory = { (db.size() >> 30) < 1 ? 16.GB : "${db.size() * 16}B"  }
    time = { (mzml.size() >> 30) < 1 ? check_max(3.h * task.attempt, 'time') : check_max((3 + (mzml.size() >> 30)) * task.attempt * 1.h, 'time') }
  }
  withName: quantLookup {
    time = { check_max( 0.1.h * isofns.size() * task.attempt, 'time' ) }
    memory = '32 GB'
    scratch = true
  }
  withName: createPSMTable {
    // lookup size is a proxy for amount of PSMs
    time = { (lookup.size() >> 30) > 1 ? check_max( 1.h * (lookup.size() >> 30) * task.attempt, 'time' ) : 2.h * task.attempt }
    // protein grouping needs memory if there's lots of information
    memory = { (lookup.size() >> 30) < 40 ? task.attempt * 16.GB : check_max("${(task.attempt * lookup.size() * 0.4).round()}B", 'memory') }
    scratch = true
  }
  withName: percolator {
    time = { listify(mzids).size() > 40 ? check_max( 0.1.h * listify(mzids).size() * task.attempt , 'time' ) : 4.h * task.attempt }
    cpus = 4
  }
  withName: DEqMS {
    cpus = 10
  }
  withName: proteinPeptideSetMerge {
    scratch = true
    // lookup size is a proxy for amount of PSMs
    time = { (lookup.size() >> 30) > 1 ? 1.h * (lookup.size() >> 30) * task.attempt : 2.h * task.attempt }
  }

  withName: luciphorPTMLocalizationScoring {
    time = { check_max( (allpsms.size() >> 20) * 0.0067.h * task.attempt, 'time') }
    cpus = 2
  }
  
  withName: countMS2perFile {
    scratch = true
  }
  withName: createPTMLookup {
    scratch = true
  }
  withName: createPTMTable {
    scratch = true
  }
  withName: mergePTMPeps {
    scratch = true
  }
  withName: createTrypticMatchDB {
    scratch = true
  }
  withName: markPeptidesPresentInDB {
    scratch = true
  }
  withName: joinAnnotatedSeqmatchPeptides {
    scratch = true
  }
  withName: PSMQC {
    scratch = true
  }
  withName: centroidMS1 {
    // network writes sometimes dont work?
    scratch = true
  }
}

workflow {
  output {
    mode = 'copy'
  }
}
