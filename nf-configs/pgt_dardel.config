external_config_version = 'main'

includeConfig "https://raw.githubusercontent.com/lehtiolab/static-resources/${external_config_version}/nf-configs/pdc_dardel.config"
includeConfig "https://raw.githubusercontent.com/lehtiolab/static-resources/${external_config_version}/nf-configs/pgt.config"

def listify(it) {
  /* This function is useful when needing a list even when having a single item
  - Single items in channels get unpacked from a list
  - Processes expect lists. Even though it would be fine
  without a list, for single-item-lists any special characters are not escaped by NF
  in the script, which leads to errors. See:
  https://github.com/nextflow-io/nextflow/discussions/4240
  */
  return it instanceof java.util.List ? it : [it]
}


process {
  withName: createNewSpectraLookup {
    time = { mzmlfiles.size() > 100 ? 0.02.h * mzmlfiles.size() * task.attempt : 2.h * task.attempt}
    memory = { mzmlfiles.size() > 1000 ? 16.GB * (mzmlfiles.size() / 1000) * task.attempt : 16.GB * task.attempt}
  }
  withName: isobaricQuant {
    memory = { (infile.size() >> 30) > 5 ? infile.size() * 4 as nextflow.util.MemoryUnit : '16 GB'}
    time = { 1.h * (infile.size() >> 30) * task.attempt }
  }

  withName: createDecoy {
    time = 6.h
  }
  withName: msgfPlus {
    cpus = { (db.size() >> 30) < 1 ? 16 : (db.size() >> 30) * 16  }
    memory = { ((canonical_fa.size() + db.size()) >> 30) < 1 ? 24.GB : "${(canonical_fa.size() + db.size()) * 24}B"  }
    time = { (mzml.size() >> 30) < 1 ? 3.h * task.attempt : (3 + (mzml.size() >> 30)) * task.attempt * 1.h }
  }

  withName: quantLookup {
    time = { 0.1.h * isofns.size() * task.attempt }
    memory = '32 GB'
  }

  withName: createPSMTable {
    // lookup size is a proxy for amount of PSMs
    time = { (lookup.size() >> 30) > 1 ? 1.h * (lookup.size() >> 30) * task.attempt : 2.h * task.attempt }
    // protein grouping needs memory if there's lots of information
    memory = { (lookup.size() >> 30) < 20 ? task.attempt * 16.GB : (lookup.size() >> 30) * 2.GB * task.attempt }
  }

  withName: percolator {
    time = { listify(mzids).size() > 40 ? 0.1.h * listify(mzids).size() * task.attempt : 4.h * task.attempt }
    cpus = 4
  }

  withName: msstitchCreateFullProteinDB {
    memory = 32.GB
  }

  // Experimental, possibly relate to size of input
  withName: bamToGtf {
    time = 12.h
    errorStrategy = 'retry'
  }

  withName: insertPeptidesDB {
    // Part of the memory is consumed by reading the
    // pI peptides file
    memory = { 64.GB + 1.GB * listify(peptides).size() }
    time = { 1.h + 0.25.h * listify(peptides).size() }
  }

  withName: joinPeptidePis {
    time = { (lookup.size() >> 30) > 1 ? 2.h * (lookup.size() >> 30) * task.attempt : 2.h * task.attempt }
    memory = 64.GB
  }

  withName: PSMQC {
    // temp solution for this, have big run that needs finishing, parametrize so that
    // it is "psmfile.size >> 30 * 1.5GB" (23GB psms works with 32GB mem)
    memory = 32.GB
  }

  withName: getDBPeptidesForTable {
    memory = {(seqdb.size() >> 30) > 3 ? seqdb.size() * 4.GB : 16.GB}

  }
  withName: writeAllPeptides {
    memory = {(seqdb.size() >> 30) > 3 ? seqdb.size() * 4.GB : 16.GB}
  }
}
