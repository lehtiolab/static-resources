external_config_version = 'main'

includeConfig "https://raw.githubusercontent.com/lehtiolab/static-resources/${external_config_version}/nf-configs/lehtio.config"

def listify(it) {
  /* This function is useful when needing a list even when having a single item
  - Single items in channels get unpacked from a list
  - Processes expect lists. Even though it would be fine
  without a list, for single-item-lists any special characters are not escaped by NF
  in the script, which leads to errors. See:
  https://github.com/nextflow-io/nextflow/discussions/4240
  */
  return it instanceof java.util.List ? it : [it]
}


process {
  withName: runThermoFileparser {
    time = { (x.size() >> 30) > 0 ? 1.h * (x.size() >> 30) * task.attempt : 1.h * task.attempt}
    memory = { (x.size() >> 30) > 0 ? 16.GB * (x.size() >> 30) * task.attempt : 16.GB * task.attempt}
    scratch = true
  }
  withName: createNewSpectraLookup {
    time = { listify(mzmlfiles).size() > 100 ? 0.02.h * listify(mzmlfiles).size() * task.attempt : 2.h * task.attempt}
    memory = { listify(mzmlfiles).size() > 1000 ? 16.GB * listify(mzmlfiles).size() / 1000 * task.attempt : 16.GB * task.attempt}
    scratch = true
  }
  withName: isobaricQuant {
    maxForks = 10
    memory = { (infile.size() >> 30) > 5 ? infile.size() * 4 as nextflow.util.MemoryUnit : '16 GB'}
    time = { 1.h * (infile.size() >> 30) * task.attempt }
  }
  withName: sage {
    maxForks = 20
    cpus = { (db.size() >> 30) < 1 ? 8 : (db.size() >> 30) * 8  }
    memory = { (db.size() >> 30) < 2 ? 32.GB : "${db.size() * 16}B"  }
    time = { (mzml.size() >> 30) < 1 ? 1.h * task.attempt : (2 + (mzml.size() >> 30)) * task.attempt * 1.h }
  }
  withName: quantLookup {
    time = { 0.1.h * listify(isofns).size() * task.attempt }
    memory = '32 GB'
    scratch = true
  }
  withName: createPSMTable {
    // lookup size is a proxy for amount of PSMs
    time = { (lookup.size() >> 30) > 1 ? 1.h * (lookup.size() >> 30) * task.attempt : 2.h * task.attempt }
    // protein grouping needs memory if there's lots of information
    memory = { (lookup.size() >> 30) < 16 ? task.attempt * 16.GB : task.attempt * (lookup.size() >> 30) * 1.GB }
    scratch = true
  }
  withName: percolator {
    time = { listify(mzids).size() > 40 ? 0.1.h * listify(mzids).size() * task.attempt : 4.h * task.attempt }
    cpus = 4
  }
}

workflow {
  output {
    mode = 'copy'
  }
}
